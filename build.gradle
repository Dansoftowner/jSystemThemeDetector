plugins {
    id 'java'
}

group 'com.jthemedetector'
version '3.8'

repositories {
    mavenCentral()
}

compileJava {
    // placing dependencies to the module path
    // https://discuss.gradle.org/t/gradle-doesnt-add-modules-to-module-path-during-compile/27382

    inputs.property("moduleName", "com.jthemedetector")
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'

    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testImplementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'

    //JNA
    implementation 'net.java.dev.jna:jna:5.15.0'
    implementation 'net.java.dev.jna:jna-platform:5.15.0'

    //JFA
    implementation ('de.jangassen:jfa:1.2.0') {
        exclude group: 'net.java.dev.jna', module: 'jna' // different jna version
    }

    //OSHI
    implementation 'com.github.oshi:oshi-core:5.8.6'

    implementation 'io.github.g00fy2:versioncompare:1.4.1'

    implementation 'org.jetbrains:annotations:22.0.0'
}

